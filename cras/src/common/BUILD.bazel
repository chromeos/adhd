# Copyright 2021 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")

cc_library(
    name = "cras_alsa_card_info",
    hdrs = [
        "cras_alsa_card_info.h",
    ],
    visibility = [
        "//cras/src/common:__pkg__",
        "//cras/src/server:__pkg__",
    ],
)

cc_library(
    name = "cras_config",
    srcs = [
        "cras_config.c",
    ],
    defines = [
        "CRAS_SOCKET_FILE_DIR=\\\"/run/cras\\\"",
    ],
    visibility = [
        "//cras/src/libcras:__pkg__",
    ],
    deps = [
        "//cras/include",
        "//cras/src/common:cras_types",
    ],
)

cc_library(
    name = "cras_file_wait",
    srcs = [
        "cras_file_wait.c",
    ],
    hdrs = [
        "cras_file_wait.h",
    ],
    visibility = [
        "//cras/src/libcras:__pkg__",
    ],
    deps = [
        "//cras/src/common:cras_config",
        "//cras/src/common:cras_string",
    ],
)

cc_library(
    name = "cras_messages",
    visibility = [
        "//cras/src/libcras:__pkg__",
    ],
    deps = ["//cras/include"],
)

cc_library(
    name = "cras_observer_ops",
    hdrs = [
        "cras_observer_ops.h",
        "//cras/common:rust_common_h_generated",
    ],
    visibility = [
        "//cras/src/libcras:__pkg__",
        "//cras/src/server:__pkg__",
    ],
)

cc_library(
    name = "cras_shm",
    srcs = [
        "cras_shm.c",
    ],
    linkopts = ["-lrt"],
    visibility = [
        "//cras/src/libcras:__pkg__",
    ],
    deps = [
        "//:build_config",
        "//cras/common:check",
        "//cras/include",
        "//cras/src/common:cras_string",
        "//cras/src/common:cras_types",
        "//cras/src/common:cras_util",
    ],
)

# Leave this one out of :cras_shm to allow unit tests to depend on cras_shm
# but stub the selinux helper.
cc_library(
    name = "cras_selinux_helper",
    srcs = select({
        "//:selinux_build": ["cras_selinux_helper.c"],
        "//conditions:default": ["cras_selinux_helper_stub.c"],
    }),
    visibility = [
        "//cras/src/libcras:__pkg__",
    ],
    deps = [":cras_shm"] + select({
        "//:selinux_build": ["@pkg_config//libselinux"],
        "//conditions:default": [],
    }),
)

cc_library(
    name = "cras_string",
    srcs = [
        "cras_string.c",
    ],
    hdrs = [
        "cras_string.h",
    ],
    visibility = [
        "//cras/src/common:__pkg__",
        "//cras/src/libcras:__pkg__",
        "//cras/src/server:__pkg__",
    ],
    deps = [
        "//third_party/strlcpy",
    ],
)

cc_library(
    name = "cras_log",
    srcs = [
        "cras_log.c",
    ],
    hdrs = [
        "cras_log.h",
    ],
    visibility = [
        "//cras/src/common:__pkg__",
        "//cras/src/libcras:__pkg__",
        "//cras/src/server:__pkg__",
        "//cras/src/tests:__pkg__",
    ],
    deps = ["//cras/common:rust_common_cc"],
)

cc_library(
    name = "cras_types",
    srcs = [
        "cras_audio_format.c",
    ],
    visibility = [
        "//cras/benchmark:__pkg__",
        "//cras/server:__pkg__",
        "//cras/src/dsp:__pkg__",
        "//cras/src/libcras:__pkg__",
        "//cras/src/server:__pkg__",
    ],
    deps = [
        "//cras/include",
    ],
)

cc_library(
    name = "cras_util",
    srcs = ["cras_util.c"],
    visibility = [
        "//cras/benchmark:__pkg__",
        "//cras/server:__pkg__",
        "//cras/src/libcras:__pkg__",
        "//cras/src/server:__pkg__",
    ],
    deps = [
        "//cras/common:check",
        "//cras/src/common:cras_types",
    ],
)

cc_library(
    name = "cras_types_internal",
    srcs = ["cras_types_internal.c"],
    hdrs = ["cras_types_internal.h"],
    visibility = ["//cras:internal"],
    deps = [
        "//cras/common:rust_common_cc",
        "//cras/include",
    ],
)

cc_test(
    name = "cras_types_internal_test",
    srcs = ["cras_types_internal_test.cc"],
    deps = [
        ":cras_types_internal",
        "@pkg_config//gtest",
        "@pkg_config//gtest_main",
    ],
)

genrule(
    name = "cras_version_h",
    outs = ["cras_version.h"],
    cmd = "echo '#define VCSID \"$(VCSID)\"' > '$@'",
)

cc_library(
    name = "cras_version",
    hdrs = ["cras_version.h"],
    visibility = ["//cras/src/libcras:__pkg__"],
)

cc_library(
    name = "blob_wrapper",
    srcs = [
        "blob_wrapper.c",
        "sof_blob_wrapper.c",
        "tlv_blob_wrapper.c",
    ],
    hdrs = [
        "blob_wrapper.h",
    ],
    visibility = [
        "//cras/src/server:__pkg__",
        "//cras/src/tests:__pkg__",
    ],
    deps = [
        "//cras/include",
    ],
)

cc_library(
    name = "common",
    srcs = [
        "array.h",
        "byte_buffer.h",
        "cras_checksum.c",
        "cras_hats.c",
        "cras_log.c",
        "cras_metrics.c",
        "cras_observer_ops.h",
        "cras_sbc_codec.c",
        "cras_sbc_codec.h",
        "dumper.c",
        "edid_utils.c",
        "edid_utils.h",
        "packet_status_logger.c",
        "sample_buffer.h",
    ],
    hdrs = [
        "bluetooth.h",
        "cras_audio_codec.h",
        "cras_checksum.h",
        "cras_hats.h",
        "cras_metrics.h",
        "dumper.h",
    ],
    visibility = [
        "//cras:__subpackages__",
    ],
    deps = [
        ":cras_alsa_card_info",
        ":cras_config",
        ":cras_file_wait",
        ":cras_log",
        ":cras_selinux_helper",
        ":cras_shm",
        ":cras_string",
        ":cras_types",
        ":cras_types_internal",
        ":cras_util",
        "//cras/dbus_bindings",
        "//cras/include",
        "//cras/src/server:cras_observer_types",
        "//third_party/bluez:a2dp_codecs",
        "//third_party/bluez:rtp",
        "//third_party/superfasthash",
        "//third_party/utlist",
        "@pkg_config//sbc",
    ] + select({
        "//:metrics_build": [
            "@pkg_config//libmetrics",
            "@pkg_config//libstructuredmetrics",
        ],
        "//conditions:default": [],
    }),
)

# Allow tests to not specify all headers.
cc_library(
    name = "all_headers",
    testonly = True,
    hdrs = glob(include = ["*.h"]),
    textual_hdrs = glob(include = ["*.c"]),  # Allow including sources
    visibility = [
        "//cras/src/tests:__pkg__",
    ],
)

# Allow tests to use sources directly
exports_files(
    glob(include = [
        "*.c",
    ]),
    visibility = [
        "//cras/src/tests:__pkg__",
    ],
)

# For distribution
exports_files(
    glob(
        include = ["*.h"],
        exclude = [
            "cras_dbus_bindings.h",
            "cras_version.h",
        ],
    ),
    visibility = ["//dist:__pkg__"],
)
